import java.util.regex.Matcher

plugins {
    id 'application'
    id 'java-library'
}


group 'de.webtwob'
version '1.0-SNAPSHOT'

sourceCompatibility = 10

ext.moduleName = "de.webtwob.the.base.game.main"
mainClassName = "$moduleName/de.webtwob.the.base.game.main.Main"

repositories {
    mavenCentral()
}

dependencies {

    compile project(':the.base.game.base')
    compile project(':the.base.game.api')

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
    runtimeOnly group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.4'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    runtimeOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.4'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    runtimeOnly group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.4'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}


run {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
            '--module-path', classpath.asPath,
            '--add-modules', 'ALL-MODULE-PATH',
            '--module', mainClassName
        ]
        classpath = files()
    }
}

startScripts {
    inputs.property("moduleName", moduleName)
    doFirst {
        classpath = files()
        defaultJvmOpts = [
                '--module-path', 'APP_HOME_LIBS',
                '--add-modules', 'ALL-MODULE-PATH',
                '--module', mainClassName
        ]
    }
    doLast {
        def bashFile = new File(outputDir, applicationName)
        String bashContent = bashFile.text
        bashFile.text = bashContent.replaceFirst('APP_HOME_LIBS', Matcher.quoteReplacement('$APP_HOME/lib'))

        def batFile = new File(outputDir, applicationName + ".bat")
        String batContent = batFile.text
        batFile.text = batContent.replaceFirst('APP_HOME_LIBS', Matcher.quoteReplacement('%APP_HOME%\\\\lib'))
    }
}
